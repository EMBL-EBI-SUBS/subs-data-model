buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
        classpath 'org.hidetake:gradle-ssh-plugin:1.1.3'
    }
}

version '1.0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'org.hidetake.ssh'

group 'uk.ac.ebi.subs'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}


dependencies {
    compileOnly("org.projectlombok:lombok:1.16.12")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("uk.ac.ebi.subs:subs-test-categories:1.0.0-SNAPSHOT")
}

task sourceJar(type: Jar) { from sourceSets.main.allJava }

task bootJar(type: BootRepackage) {
}

artifacts {
    archives jar
    archives sourceJar
}

task('externalCiTest', type: Test) {
    useJUnit {
        excludeCategories 'uk.ac.ebi.subs.RabbitMQDependentTest', 'uk.ac.ebi.subs.BioSamplesDependentTest'
    }
    testLogging {
        exceptionFormat = 'full'
    }
}

configure(externalCiTest) {
    group = "verification"
    description = 'Run tests appropriate for our external CI environment'
}

uploadArchives {
    repositories {
        mavenDeployer {

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

            }

            pom.project {
                packaging 'jar'
                // optionally artifactId can be defined here
                url 'https://github.com/EMBL-EBI-SUBS'

                scm {
                    url 'https://github.com/EMBL-EBI-SUBS'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'neilg'
                        name 'Neil Goodgame'
                        email 'neilg@ebi.ac.uk'
                    }
                }
            }
        }
    }
}

bootRepackage.enabled = false