buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'maven-publish'
}

apply plugin: 'io.spring.dependency-management'

group 'uk.ac.ebi.subs'
version '2.14.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}


dependencies {
    implementation("com.fasterxml.jackson.core:jackson-databind")

    compileOnly("org.projectlombok:lombok:1.18.10")
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

task withoutSourceJar(type: Jar) { from sourceSets.main.allJava }

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'subs-data-model'

            artifact withoutSourceJar
            artifact sourceJar

            pom {
                name = 'subs-data-model'
                description = 'Data Submission Portal - data model'
                url = 'https://github.com/EMBL-EBI-SUBS/subs-data-model'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subs-internal'
                        name = 'EMBL-EBI-SUBS'
                        email = 'subs-internal@ebi.ac.uk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/EMBL-EBI-SUBS/subs-data-model.git'
                    developerConnection = 'scm:git:ssh://github.com/EMBL-EBI-SUBS/subs-data-model.git'
                    url = 'https://github.com/EMBL-EBI-SUBS/subs-data-model.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

bootJar {
    enabled = false
}