buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.21.RELEASE")
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '1.5.21.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

group 'uk.ac.ebi.subs'
version '2.16.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

bootRepackage.enabled = false

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    mavenLocal()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation("com.fasterxml.jackson.core:jackson-databind:2.10.0")

    compileOnly("org.projectlombok:lombok:1.18.10")
    annotationProcessor 'org.projectlombok:lombok:1.18.10'

    testCompile("org.springframework.boot:spring-boot-starter-test")
}

task jarWithAddedManifest(type: Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Source-Compatibility': project.sourceCompatibility)
    }
    with jar
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'subs-data-model'

            pom {
                name = 'subs-data-model'
                description = 'Data Submission Portal - common library for data models'
                url = 'https://github.com/EMBL-EBI-SUBS/subs-data-model'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'subs-internal'
                        name = 'EMBL-EBI-SUBS'
                        email = 'subs-internal@ebi.ac.uk'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/EMBL-EBI-SUBS/subs-data-model.git'
                    developerConnection = 'scm:git:ssh://github.com/EMBL-EBI-SUBS/subs-data-model.git'
                    url = 'https://github.com/EMBL-EBI-SUBS/subs-data-model.git'
                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
